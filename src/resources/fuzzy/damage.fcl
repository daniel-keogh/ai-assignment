// This function will determine how much damage ought to be inflicted upon the player.
FUNCTION_BLOCK getDamage

    // The input variables are the enemy's strength and current energy level.
    VAR_INPUT
        strength : REAL;
        energy : REAL;
    END_VAR

    // The output will be the amount of damage.
    VAR_OUTPUT
        damage : REAL;
    END_VAR

    // The character's strength can be an arbitrary number between 1-100.
    FUZZIFY strength
        TERM weak := (0,1) (15,1) (40,0);
        TERM average := trape 25 40 60 75;
        TERM formidable := (60,0) (85,1) (100,1);
    END_FUZZIFY

    // Energy is also an arbitrary number between 1-100, but this value will
    // change as they move around the game view.
    FUZZIFY energy
        TERM low := (0,1) (20,1) (40,0);
        TERM normal := trape 30 45 55 70;
        TERM high := (60,0) (80,1) (100,1);
    END_FUZZIFY

    // Determine the output of the fuzzy inference.
    DEFUZZIFY damage
        TERM low := (0,1) (20,1) (40,0);
        TERM medium := trian 20 50 80;
        TERM high := (60,0) (80,1) (100,1);

        METHOD : COG; // Center of Gravity (Mamdani Method).
	    DEFAULT := 0;
    END_DEFUZZIFY

    // Define the set of fuzzy rules.
    RULEBLOCK No1
        AND : MIN;
        ACT : MIN;
        ACCU : MAX;

        RULE 1 : IF strength IS weak OR energy IS low THEN damage IS low;
        RULE 2 : IF strength IS average OR energy IS normal THEN damage IS medium;
        RULE 3 : IF strength IS formidable AND energy IS low THEN damage IS medium;
        RULE 4 : IF strength IS formidable AND energy IS NOT low THEN damage IS high;
    END_RULEBLOCK

END_FUNCTION_BLOCK
